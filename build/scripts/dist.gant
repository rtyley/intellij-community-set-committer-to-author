import org.jetbrains.jps.Jps
import static org.jetbrains.jps.idea.IdeaProjectLoader.*

includeTargets << new File("${guessHome(this)}/build/scripts/utils.gant")
includeTool << Jps

requireProperty("out", "$home/out")

class Paths {
  final sandbox
  final distWin
  final distAll
  final distUnix
  final distMac
  final artifacts

  def Paths(String out) {
    sandbox = out
    distWin = "$sandbox/dist.win.ce"
    distAll = "$sandbox/dist.all.ce"
    distUnix = "$sandbox/dist.unix.ce"
    distMac = "$sandbox/dist.mac.ce"

    artifacts = "$sandbox/artifacts"
  }
}

setProperty("paths", new Paths(out))

def paths = new Paths(out)

def includeFile(String filepath) {
  Script s = groovyShell.parse(new File(filepath))
  s.setBinding(binding)
  s
}

target(compile: "Compile project") {
  loadProject()

  project.stage("Cleaning up sandbox folder")
  ant.delete(dir: paths.sandbox)

  [paths.sandbox, paths.distWin, paths.distAll, paths.distUnix, paths.distMac, paths.artifacts].each {
    ant.mkdir(dir: it)
  }

  ant.tstamp() {
    format(property: "today.year", pattern: "yyyy")
  }

  project["javac"] = "$jdkHome/bin/javac"
  project.targetFolder = "$out/classes"
  project.clean()
  project.makeProduction()
}

String appInfoFile() {
  return "${this["community-resources"].output}/idea/IdeaApplicationInfo.xml"
}

def wire_build_date(String buildNumber) {
  ant.replace(token: "__BUILD_NUMBER__", value: buildNumber, file: appInfoFile())
  ant.replace(token: "__BUILD_DATE__", value: DSTAMP, file: appInfoFile())
}

target('default': 'The default target') {
  depends(compile)
  layoutAll(
          buildNumber: "IC-90.SNAPSHOT",
          version: "Maia",
          system_selector: "IntelliJIdeaCE10",
          jdk_req: "1.6*",
          vmoptions: "-Xms128m -Xmx512m -XX:MaxPermSize=250m -ea",
          home, out)
}

public def layoutAll(Map args, String home, String out) {
  Paths paths = new Paths(out)

  wire_build_date(args.buildNumber)
  ant.xmlproperty(file: appInfoFile(), collapseAttributes: "true")

  def layouts = includeFile("$home/build/scripts/layouts.gant")
  layouts.layoutFull(home, paths.distAll)

  ant.echo(message: args.buildNumber, file: "$paths.distAll/build.txt")
  ant.copy(todir: "$paths.distAll/license", preservelastmodified: "true") {
    fileset(dir: "$home/license")
  }

  ant.copy(file: "$home/LICENSE.txt", todir: "$paths.distAll", preservelastmodified: "true")

  layoutMac(args, home, paths)
  layoutLinux(args, home, paths)
  layoutWin(args, home, paths)
}

boolean isEap() {
  return "true" == p("component.version.eap")
}

private def patchPropertiesFile(String where) {
  ant.echo(file: "$where/bin/idea.properties", append: "true", message:
           isEap() ?
"""

#-----------------------------------------------------------------------
# Change to 'disabled' if you need not to receive instant visual notifications about
# fatal errors that happen to IDEA or plugins installed.
#-----------------------------------------------------------------------
idea.fatal.error.notification=enabled
"""

 :

"""

#-----------------------------------------------------------------------
# Change to 'eanbled' if you need to receive instant visual notifications about
# fatal errors that happen to IDEA or plugins installed.
#-----------------------------------------------------------------------
idea.fatal.error.notification=disabled
"""
  )
}

private def layoutWin(Map args, String home, Paths paths) {
  layout("$paths.distWin") {
    dir("bin") {
      fileset(dir: "$home/bin") { include(name: "*.*") }
      fileset(dir: "$home/bin/win") { include(name: "*.*") }
    }
  }

  patchPropertiesFile(paths.distWin)
  ant.echo(file: "$paths.distWin/bin/idea.exe.vmoptions", message: args.vmoptions.replace(' ', '\n'))

  ant.zip(zipfile: "$paths.artifacts/idea${args.buildNumber}.win.zip") {
    fileset(dir: paths.distAll)
    fileset(dir: paths.distWin)
  }
}

private def layoutMac(Map args, String home, Paths paths) {
  project.stage("Cleaning up sandbox folder")

  ant.copy(todir: "$paths.distMac/bin") {
    fileset(dir: "$home/bin") { include(name: "*.*") }
    fileset(dir: "$home/bin/nix")
    fileset(dir: "$home/bin/mac")
  }

  patchPropertiesFile(paths.distMac)

  ant.copy(todir: paths.distMac) {
    fileset(dir: "$home/build/conf/mac")
  }

  def version = isEap() ? p("component.version.codename") : p("component.version.major") + "." +  p("component.version.minor")

  ant.replace(file: "${paths.distMac}/Contents/Info.plist") {
    replacefilter(token: "@@build@@", value: args.buildNumber)

    replacefilter(token: "@@version@@", value: version)
    replacefilter(token: "@@vmoptions@@", value: args.vmoptions + " -Xverify:none -XX:+UseCompressedOOPS")
    replacefilter(token: "@@system_selector@@", value: args.system_selector)
    replacefilter(token: "@@jdk_req@@", value: args.jdk_req)
  }

  def root = isEap() ? "${version}-${args.buildNumber}.app" : "IntelliJ IDEA ${version} CE.app"

  ant.zip(zipfile: "$paths.artifacts/idea${args.buildNumber}.mac.zip") {
    [paths.distAll, paths.distMac].each {
      tarfileset(dir: it, prefix: root) {
        exclude(name: "bin/*.sh")
        exclude(name: "Contents/MacOS/idea")
      }
    }

    tarfileset(dir: paths.distMac, filemode: "755", prefix: root) {
      include(name: "bin/*.sh")
      include(name: "Contents/MacOS/idea")
    }
  }
}

def layoutLinux(Map args, String home, Paths paths) {
  project.stage("tar.gz")
  layout(paths.distUnix) {
    dir("bin") {
      fileset(dir: "$home/bin") { include(name: "*.*") }
      fileset(dir: "$home/bin/linux") { include(name: "*.*") }
      fileset(dir: "$home/bin/nix") { include(name: "*.*") }
    }
  }

  patchPropertiesFile(paths.distUnix)

  ant.echo(file: "$paths.distUnix/bin/idea.vmoptions", message: args.vmoptions.replace(' ', '\n') )
  ant.fixcrlf(srcdir: "$paths.distUnix/bin", includes: "idea.vmoptions", eol: "unix")
  ant.fixcrlf(srcdir: "$paths.distUnix/bin", includes: "*.sh", eol: "unix")


  def tarRoot = "idea-$args.buildNumber"
  def tarPath = "$paths.artifacts/idea${args.buildNumber}.tar"
  ant.tar(tarfile: tarPath) {
    [paths.distAll, paths.distUnix].each {
      tarfileset(dir: it, prefix: tarRoot) {
        exclude(name: "bin/*.sh")
      }
    }

    tarfileset(dir: paths.distUnix, filemode: "755", prefix: tarRoot) {
      include(name: "bin/*.sh")
    }
  }

  ant.gzip(src: tarPath, zipfile: "${tarPath}.gz")
  ant.delete(file: tarPath)
}

